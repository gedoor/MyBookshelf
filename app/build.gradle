apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'io.fabric'

static def releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

def name = "MONKOVEL"
def version = "2." + releaseTime()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    signingConfigs {
        myConfig {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_KEY_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_STORE_PASSWORD
        }
    }

    defaultConfig {
        applicationId "com.gedoor.monkeybook"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 10000 + gitCommits
        versionName version
        project.ext.set("archivesBaseName", name + "_" + version)
        multiDexEnabled true

    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            buildConfigField "boolean", "IS_RELEASE", "true"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.myConfig
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
            buildConfigField "boolean", "IS_RELEASE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${name}_${defaultConfig.versionName}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

}

dependencies {
    api project(':basemvplib')
    testImplementation 'junit:junit:4.12'

    //fireBase
    //noinspection GradleDependency
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'

    //support
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:support-compat:$support_library_version"
    implementation "com.android.support:support-media-compat:$support_library_version"
    implementation "com.android.support:support-v4:$support_library_version"
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.android:flexbox:1.0.0'

    //GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.1.0'

    //codecMD5
    implementation 'commons-codec:commons-codec:1.11'

    //ProgressBar
    implementation 'com.zhangmonke:MProgressBar:1.0.1'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    //AutoFitTextView
    implementation 'me.grantland:autofittextview:0.2.1'

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    //bind view
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //动画
    implementation 'com.victor:lib:1.0.4'
    implementation 'com.github.gtmora200:ZExplosionField:1.0.3'

    //简易权限获取
    implementation 'pub.devrel:easypermissions:2.0.0'

    //二维码
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'

    //颜色选择
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'

    //文件目录选择
    implementation('com.github.gedoor.AndroidPicker:FilePicker:1.6.3') {
        exclude group: 'com.android.support'
    }

    //简繁转换
    implementation 'com.luhuiguo:chinese-utils:1.0'

    //字符串比较
    implementation 'net.ricecode:string-similarity:1.0.0'

    //MarkDown
    implementation 'ru.noties:markwon:2.0.0'

    //epub
    implementation('nl.siegmann.epublib:epublib-core:3.1') {
        exclude group: 'xmlpull'
    }
}

greendao {
    schemaVersion 51
    daoPackage 'com.kunfei.bookshelf.dao'
    targetGenDir 'src/main/java'
}

apply plugin: 'com.google.gms.google-services'
