apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'io.fabric'

static def releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

def name = "YueDu"
def version = "2." + releaseTime()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    signingConfigs {
        myConfig {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_KEY_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_STORE_PASSWORD
        }
    }

    defaultConfig {
        applicationId "com.gedoor.monkeybook"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 10000 + gitCommits
        versionName version
        project.ext.set("archivesBaseName", name + "_" + version)
        multiDexEnabled true

    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            signingConfig signingConfigs.myConfig
            buildConfigField "boolean", "IS_RELEASE", "true"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.myConfig
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
            buildConfigField "boolean", "IS_RELEASE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${name}_${defaultConfig.versionName}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':basemvplib')
    testImplementation 'junit:junit:4.12'

    //fireBase
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    //androidX
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.media:media:1.1.0-alpha04'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //google
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.android:flexbox:1.1.0'

    //J_SOUP
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'cn.wanghaomiao:JsoupXpath:2.3.2'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'
    implementation 'io.apisense:rhino-android:1.0'

    //GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.1.0'

    //codecMD5
    implementation 'commons-codec:commons-codec:1.11'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.0.0'

    //bind view
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    //动画
    implementation 'com.victor:lib:1.0.4'

    //二维码
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'

    //颜色选择
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    //文件目录选择
    implementation 'com.github.gedoor.AndroidPicker:FilePicker:2.0.0'

    //简繁转换
    implementation 'com.luhuiguo:chinese-utils:1.0'

    //字符串比较
    implementation 'net.ricecode:string-similarity:1.0.0'

    //MarkDown
    implementation 'ru.noties.markwon:core:3.0.0'

    //epub
    implementation('nl.siegmann.epublib:epublib-core:3.1') {
        exclude group: 'org.slf4j'
        exclude group: 'xmlpull'
    }

}

greendao {
    schemaVersion 55
    daoPackage 'com.kunfei.bookshelf.dao'
    targetGenDir 'src/main/java'
}

apply plugin: 'com.google.gms.google-services'
