apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'io.fabric'

static def increasedVersionCode() {
    return (int) (System.currentTimeMillis() / 1000 / 60 / 60 - 17141)
}

def name = "MONKOVEL"
def version = "1.6." + increasedVersionCode()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def gitTag = "git tag " + version

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.gedoor.monkeybook"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 10000 + gitCommits
        versionName version
        project.ext.set("archivesBaseName", name + "_" + version)
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            buildConfigField "boolean", "IS_RELEASE", "true"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
            buildConfigField "boolean", "IS_RELEASE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${name}_${defaultConfig.versionName}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    sourceSets { main { resources.srcDirs = ['src/main/resources', 'src/main/resources/'] } }

}

ext {
    support_library_version = '27.1.1'
}
dependencies {
    api project(':basemvplib')
    testImplementation 'junit:junit:4.12'

    //implementation fileTree(include: ['*.jar'], dir: 'libs')

    //fireBase
    implementation 'com.google.firebase:firebase-core:15.0.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }

    //support
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:support-compat:$support_library_version"
    implementation 'com.google.code.gson:gson:2.8.2'

    //GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.0.1'

    //codecMD5
    implementation 'commons-codec:commons-codec:1.11'

    //沉浸式状态栏
    implementation 'com.zhangmonke:ImmerseLayout:1.1.1'

    //ProgressBar
    implementation 'com.zhangmonke:MProgressBar:1.0.1'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
    implementation 'jp.wasabeef:glide-transformations:3.2.0'
    // If you want to use the GPU Filters
    implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'

    //AutoFitTextView
    implementation 'me.grantland:autofittextview:0.2.1'

    //删除粒子效果
    implementation 'tyrantgit:explosionfield:1.0.1'

    //View简易动画
    implementation 'com.daimajia.easing:library:2.1@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    //SwitchButton
    implementation 'com.kyleduo.switchbutton:library:2.0.0'

    //bind view
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //动画
    implementation 'com.victor:lib:1.0.4'

    //简易权限获取
    implementation 'pub.devrel:easypermissions:1.2.0'

    //圆形按钮
    implementation 'com.github.markushi:circlebutton:1.1'

    //二维码
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'

    //颜色选择
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'

    //简繁转换
    implementation 'com.luhuiguo:chinese-utils:1.0'


}

greendao {
    schemaVersion 19
    daoPackage 'com.monke.monkeybook.dao'
    targetGenDir 'src/main/java'
}


apply plugin: 'com.google.gms.google-services'
